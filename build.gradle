plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.openapi.generator' version '7.14.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

ext {
    BP_SPRING_AOT_ENABLED = 'false'
    BP_JVM_CDS_ENABLED = 'false'
}


java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}


repositories {
	mavenCentral()
}

dependencies {
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.35'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// OpenAPI Generator configuration
openApiGenerate {
    generatorName = 'spring'
    inputSpec = "${projectDir}/src/main/resources/openapi/student.yml"
    outputDir = "${buildDir}/generated"
    apiPackage = 'com.example.students.api'
    modelPackage = 'com.example.students.model'
    configOptions = [
        interfaceOnly: 'true',
        useSpringBoot3: 'true'
    ]
}

// Ensure generated sources are included in compilation
sourceSets {
    main {
        java {
            srcDirs += "${buildDir}/generated/src/main/java"
        }
    }
}


compileJava.dependsOn tasks.openApiGenerate


tasks.bootBuildImage {
    environment = [
            BP_SPRING_AOT_ENABLED : BP_SPRING_AOT_ENABLED,
            BP_JVM_CDS_ENABLED    : BP_JVM_CDS_ENABLED,
            BP_JVM_VERSION        : JavaVersion.VERSION_21
    ]
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
